mod std::concurrency::thread;
import std::io, std::concurrency::message;
import std::any;
import core::typeinfo, core::duplication;


/**
 * Lib pthread, create a thread
 */
extern (C) def _yrt_thread_create (mut thread : &(mut usize), attr : &void, call : fn (mut Thread)-> void, mut data : Thread);

/**
 * Wait the end of a thread
 */
extern (C) def _yrt_thread_join (thread : usize, attr : &void);

/**
 * Interrupt the thread
 */
extern (C) def _yrt_thread_cancel (thread : usize, attr : &void);

/**
 * Class and pointer have the same size, so there is no problem for pthread
 * And the class will be ok to run
 */
def threadFunc (mut actor : Thread) -> void {
    (alias actor).run ();
}

/**
 * Example : 
 * =================
 * mod main;
 *
 * import std::concurrency::actor;
 * import std::io;
 *
 * extern (C) def usleep (time : u32);
 *
 * class MyThread over Actor {
 *
 *    self () {}
 *  
 *    over run (mut self) {
 *        usleep (1000_000_u32);
 *        println ("Ready to recv message !");
 *        loop {
 *            let msg = self.recv ();
 *            println (msg);
 *        }
 *     }
 * }
 *
 *
 * def main () {
 *     let mut x = MyThread::new ();
 *     (alias x).spawn ();
 *     let dmut array = [1, 2, 3, 4, 5];
 *     loop {
 *         x.send (array);
 *         usleep (1000_000_u32);
 *     }
 *
 *     array [0] = 9;
 *     (alias x).join ();
 * }
 * ====================
 * A thread is a sequence of instruction that are managed independently
 * They are used to perform asynchronus operation
 */
pub class Thread {

    let mut _id = 0us;

    let mut _pipe = Pipe::new ();
    
    pub self () {}

    /**
     * The things to do in the thread
     * This method is launched in the thread, and is the entry point of the thread
     */
    pub def run (mut self)-> void {}

    /**
     * Send a message to the thread
     * The message will be encapsulated in an Any type (Cf. std::any)
     * The thread that is performing the send, will be blocked until the Thread is in recv state
     */
    pub def send (T) (self, x : T) {
        let mut value = Some!(T)::new (x);       
        self._pipe.write (value);
    }

    /**
     * Recv a message using the pipe of this thread
     * Normally this function is used within the context of the run method
     * Block the execution until the thread effectively received something
     */
    pub def recv (self)-> Any {
        self._pipe.read!(Any)()
    }

    pub def getId (self) -> usize 
        self._id
    
    /**
     * Start the thread
     */
    pub def spawn (mut self) {
        _yrt_thread_create (alias (&self._id), null, &threadFunc, alias self);
    }

    pub def join (self) {
        _yrt_thread_join (self._id, null);
    }

    pub def kill (mut self) {
        _yrt_thread_cancel (self._id, null);
    }
    
}
