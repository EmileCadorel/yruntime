mod core.array;
import std.abort;
import std.traits;

struct (T)
| len : u64
| ptr : p!T
 -> slice;

def print (T) (a : const (slice!(const T))) {
    let aux = const [T] {a.len, a.ptr};
    print (aux);
}

def opEquals (T) (const left : [T], const right : [T]) -> bool {
    if left.len != right.len
        return false;
    
    for it in 0UL .. left.len {
        if left [it] != right [it]
            return false;
    }
    return true;
}

def opIndex (T, T2) (a : const (slice!T),  i : T2) -> T {
    if i < a.len {
        return *(a.ptr + (cast!u64(i) * T::sizeof));
    } else {
        abort ();
        return *a.ptr;
    }
}

def opIndex (T, T2) (a : slice!T,  i : u32) -> ref T {
    if i < a.len {
        return *(a.ptr + (cast!u64 (i) * T::sizeof));
    } else {
        abort ();
        return *a.ptr;
    }
}

def opIndex (T2, T) (a : [T2], i : r!(T)) -> mut slice!T2 {
    if i.scd >= i.fst && (i.scd - i.fst) <= a.len && i.fst <= a.len {
        return slice!T2 {cast!u64 (i.scd - i.fst), a.ptr + (cast!u64 (i.fst) * T2::sizeof)};
    } else {
        abort ();
        return slice!T2 {0UL, null};
    }
}

def opIndex (T2, T) (a : const [T2], i : r!(T)) -> slice!T2 {
    if i.scd >= i.fst && (i.scd - i.fst) <= a.len && i.fst <= a.len {
        return const (slice!T2) {cast!u64 (i.scd - i.fst), a.ptr + (cast!u64 (i.fst) * T2::sizeof)};
    } else {
        assert (false, "Out of range");
    }
}

def opIndex (T) (a : [T]) -> mut slice!(mut T) {
    return slice!T {a.len, a.ptr};
}

def opIndex (T) (a : [const T]) -> slice!(const T) {
    return const (slice!T) {a.len, a.ptr};
}

def replicate (T) (const array : [T], ntimes : u64) -> [mut T]{
    let res = [T ; new array.len * cast!u64 (ntimes)];
    for i in 0U .. ntimes {
        for j in 0U .. array.len {
            res [i * array.len + j] = array [j];
        }
    }
    return res;
}

def opBinary ("+", T) (left : const slice!T,  right : const slice!T) -> slice!T {
    assert (left.len == right.len, "size mistmatch");
    let res = [mut T ; new left.len];
    for i in 0U .. left.len {
        res [i] = left [i] + right [i];
    }
    return slice!T {res.len, res.ptr};
}

def dup (T) (const array : [const T]) -> [mut T] {
    let res = [mut T ; new array.len];
    for it in 0U .. array.len {
        res [it] = array [it];
    }
    return res;
}

def dup (T, N : u64) (ref array : const ([const T ; N])) -> [mut T] {
    let res = [mut T ; new array.len];
    for it in 0U .. array.len {
        res [it] = array [it];
    }
    return res;
}

def dup (T) (const array : slice!T) -> [mut T] {
    let res = [T ; new array.len];
    for it in 0U .. array.len {
        res [it] = array [it];
    }
    return res;
}

def array (U) (a : const slice!U) -> [U] {
    return const![U] {a.len, a.ptr};
}

def array (U) (ref a : slice!U) -> mut [U] {
    return [U] {a.len, a.ptr};
}

